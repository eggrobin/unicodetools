# This file uses the invariant test language, but contains comparisons between
# new and pre-existing characters to aid in PAG review of encoding proposals.

# Names always differ.
# Age always differs since these tests are comparing additions to pre-existing characters.
Ignoring Name Age:

## Unicode 16.0 additions.

# These comparisons were not in place when properties were initially assigned for the 16.0
# répertoire.
# We note here the feedback about errors that would have been caught by them.

# U+18CFF is a blank character for the Khitan Small Script; aside from looking blank,
# it is indistinguishable from other Khitan Small Script characters.  See L2/23-065.
# In particular, it is ideographic:
# https://www.unicode.org/review/pri497/feedback.html#ID20240216140104.
Propertywise [\N{KHITAN SMALL SCRIPT CHARACTER-18CFF}
              \N{KHITAN SMALL SCRIPT CHARACTER-18B00}] AreAlike

# HXG (briefly known as HZXG) and SZP are just like all the other CJK strokes.
# In particular, they are scx=Hani:
# https://www.unicode.org/review/pri502/feedback.html#ID20240523095709.
Propertywise [\N{CJK STROKE HXG}\N{CJK STROKE SZP}
              \N{CJK STROKE T}] AreAlike

# Garay is a right-to-left cased script:
Propertywise [\N{GARAY SMALL LETTER A} - \N{GARAY SMALL LETTER OLD NA}]
           : [\N{GARAY CAPITAL LETTER A} - \N{GARAY CAPITAL LETTER OLD NA}]
CorrespondTo [\N{OLD HUNGARIAN SMALL LETTER A}]
           : [\N{OLD HUNGARIAN CAPITAL LETTER A}]
    UpTo: Block             (Garay vs Old_Hungarian),
          Script            (Garay vs Old_Hungarian),
          Script_Extensions (Garay vs Old_Hungarian)

## Provisionally assigned. [placeholder for draft PRs]
# Ignore the security and IDNA properties, as these are not yet included for provisionally assigned characters.
Ignoring Confusable_MA Identifier_Status Identifier_Type Idn_Status Idn_Mapping:

Ignoring Block:
# The range \N{MODIFIER LETTER SMALL CAPITAL A}-\N{MODIFIER LETTER SMALL T WITH CURL} is split in
# three tests because the modifiers are encoded in the order (small capitals, fishhooks, curls), but
# the non-modifiers are in code point order (curls, fishhooks, small capitals).
Propertywise [ \N{MODIFIER LETTER SMALL CAPITAL A}
               \N{MODIFIER LETTER SMALL CAPITAL E}]
           : [ᴀ\N{LATIN LETTER SMALL CAPITAL A}
              ᴇ\N{LATIN LETTER SMALL CAPITAL E}]
CorrespondTo [ʶ\N{MODIFIER LETTER SMALL CAPITAL INVERTED R}]
           : [ʁ\N{LATIN LETTER SMALL CAPITAL INVERTED R}]

Ignoring Unicode_1_Name:  # ɿ has a Unicode_1_Name (FISHHOOK R instead of R WITH FISHHOOK).
Propertywise [ \N{MODIFIER LETTER SMALL REVERSED R WITH FISHHOOK}
               \N{MODIFIER LETTER SMALL SQUAT REVERSED ESH}
               \N{MODIFIER LETTER SMALL TURNED H WITH FISHHOOK}
               \N{MODIFIER LETTER SMALL TURNED H WITH FISHHOOK AND TAIL}]
           : [ɿ\N{LATIN SMALL LETTER REVERSED R WITH FISHHOOK}
              ʅ\N{LATIN SMALL LETTER SQUAT REVERSED ESH}
              ʮ\N{LATIN SMALL LETTER TURNED H WITH FISHHOOK}
              ʯ\N{LATIN SMALL LETTER TURNED H WITH FISHHOOK AND TAIL}]
CorrespondTo [ʶ\N{MODIFIER LETTER SMALL CAPITAL INVERTED R}]
           : [ʁ\N{LATIN LETTER SMALL CAPITAL INVERTED R}]
end Ignoring;

Propertywise [ \N{MODIFIER LETTER SMALL D WITH CURL}
               \N{MODIFIER LETTER SMALL L WITH CURL}
               \N{MODIFIER LETTER SMALL N WITH CURL}
               \N{MODIFIER LETTER SMALL T WITH CURL}]
           : [ȡ\N{LATIN SMALL LETTER D WITH CURL}
              ȴ\N{LATIN SMALL LETTER L WITH CURL}
              ȵ\N{LATIN SMALL LETTER N WITH CURL}
              ȶ\N{LATIN SMALL LETTER T WITH CURL}]
CorrespondTo [ʶ\N{MODIFIER LETTER SMALL CAPITAL INVERTED R}]
           : [ʁ\N{LATIN LETTER SMALL CAPITAL INVERTED R}]
end Ignoring;

end Ignoring;

end Ignoring;